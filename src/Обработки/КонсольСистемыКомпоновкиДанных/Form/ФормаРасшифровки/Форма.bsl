&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ДанныеРасшифровкиОбъект;
	
	РеквизитОбъект = РеквизитФормыВЗначение("Отчет");
	ИмяФормыРасшифровки = РеквизитОбъект.Метаданные().ПолноеИмя() + ".Форма.ФормаРасшифровки";
	
	СтандартнаяОбработка = Ложь;
	Если Параметры.Расшифровка <> Неопределено Тогда
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(Параметры.АдресСхемыКомпоновкиДанных);
		АдресСхемыИсполненногоОтчета = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
		ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыИсполненногоОтчета);
		Отчет.КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
		ДанныеРасшифровкиОбъект = ПолучитьИзВременногоХранилища(Параметры.Расшифровка.Данные);
		ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровкиОбъект, ИсточникДоступныхНастроек);
		ПрименяемыеНастройки = ОбработкаРасшифровки.ПрименитьНастройки(Параметры.Расшифровка.Идентификатор, Параметры.Расшифровка.ПрименяемыеНастройки);
		Если ТипЗнч(ПрименяемыеНастройки) = Тип("НастройкиКомпоновкиДанных") Тогда
			Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(ПрименяемыеНастройки);
		ИначеЕсли ТипЗнч(ПрименяемыеНастройки) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
			Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(ДанныеРасшифровкиОбъект.Настройки);
			Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПрименяемыеНастройки);
		КонецЕсли;
		
		ВыполнитьНаСервере(СхемаКомпоновкиДанных);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВыполнитьНаСервере(СхемаКомпоновкиДанных_)
	Перем ДанныеРасшифровкиОбъект;
	
	Результат.Очистить();
	
	СхемаКомпоновкиДанных = СхемаКомпоновкиДанных_;
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыИсполненногоОтчета);
	КонецЕсли;
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, Отчет.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровкиОбъект);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровкиОбъект);
	
	ПроцессорВыводаРезультатаОтчета = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаРезультатаОтчета.УстановитьДокумент(Результат);
	ПроцессорВыводаРезультатаОтчета.НачатьВывод();
	ПроцессорВыводаРезультатаОтчета.Вывести(ПроцессорКомпоновкиДанных);
	ПроцессорВыводаРезультатаОтчета.ЗакончитьВывод();
	
	АдресДанныхРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиОбъект, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Перем ВыбранноеДействие, ПараметрыВыбранногоДействия;
	
	СтандартнаяОбработка = Ложь;
	
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(АдресДанныхРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыИсполненногоОтчета));
	ОбработкаРасшифровки.ВыбратьДействие(Расшифровка, ВыбранноеДействие, ПараметрыВыбранногоДействия);
	Если ВыбранноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет Тогда
	ИначеЕсли ВыбранноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		ОткрытьЗначение(ПараметрыВыбранногоДействия);
	Иначе
		ОткрытьФорму(ИмяФормыРасшифровки, Новый Структура("Расшифровка,АдресСхемыКомпоновкиДанных", Новый ОписаниеОбработкиРасшифровкиКомпоновкиДанных(АдресДанныхРасшифровки, Расшифровка, ПараметрыВыбранногоДействия), АдресСхемыИсполненногоОтчета), , Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВариант(Команда)
	Форма = ПолучитьФорму("ВнешнийОтчет.КонсольСистемыКомпоновкиДанных.Форма.ФормаВарианта", Новый Структура("Вариант,АдресСхемыИсполненногоОтчета", Отчет.КомпоновщикНастроек.Настройки,АдресСхемыИсполненногоОтчета));
	Если Форма.ОткрытьМодально() = Истина Тогда
		ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Форма.Отчет.КомпоновщикНастроек.Настройки);
		Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	ВыполнитьНаСервере(Неопределено);
КонецПроцедуры
