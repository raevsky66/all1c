Перем мОчищатьТаблицуПередЗаполнением Экспорт;
Перем мВыполнитьЗаполнение Экспорт;

Процедура Инициализировать(Объект, ИмяТабличнойЧасти, ТабличноеПолеОбъекта) Экспорт
	
	ТабЧасть	= Объект[ИмяТабличнойЧасти];
	
	ДатаНач = "";
	ВвестиДату(ДатаНач,"Введите начальную дату",ЧастиДаты.Дата);
	
	ДатаКон = "";
	ВвестиДату(ДатаКон,"Введите конечную дату",ЧастиДаты.Дата);
	
	Транспорт = Справочники.ТранспортныеСредства.ПустаяСсылка();
	ВвестиЗначение(Транспорт,"Укажите группу транспорта");
	
	
	Если НЕ ЗначениеЗаполнено(ДатаНач) ИЛИ Не ЗначениеЗаполнено(ДатаКон) Тогда 
		Предупреждение("Не указаны даты...");
		возврат;
	КонецЕсли;
	
	ДатаКон = КонецДня(ДатаКон);
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Сообщить("Не выбрана организация");
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Транспорт) Тогда
		Сообщить("Не выбрана группа транспорта");
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Ссылка.Дата, ДЕНЬ) КАК Дата,
	               	|	РеализацияТоваровУслуг.Ссылка.ТранспортноеСредство,
	               	|	РеализацияТоваровУслуг.Ссылка.Контрагент,
	               	|	СУММА(1) КАК Количество
	               	|ПОМЕСТИТЬ Отгрузки
	               	|ИЗ
	               	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	               	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураСДоставкой КАК НоменклатураСДоставкой
	               	|		ПО РеализацияТоваровУслуг.Номенклатура = НоменклатураСДоставкой.Номенклатура
	               	|ГДЕ
	               	|	РеализацияТоваровУслуг.Ссылка.Дата МЕЖДУ &НачДата И &КонДата
	               	|	И РеализацияТоваровУслуг.Ссылка.ВнутреннийНомерНакладной > 0
	               	|	И РеализацияТоваровУслуг.Ссылка.ТранспортноеСредство В ИЕРАРХИИ(&Транспорт)
	               	|	И РеализацияТоваровУслуг.Ссылка.Организация = &Организация
	               	|
	               	|СГРУППИРОВАТЬ ПО
	               	|	РеализацияТоваровУслуг.Ссылка.ТранспортноеСредство,
	               	|	РеализацияТоваровУслуг.Ссылка.Контрагент,
	               	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Ссылка.Дата, ДЕНЬ)
	               	|;
	               	|
	               	|////////////////////////////////////////////////////////////////////////////////
	               	|ВЫБРАТЬ
	               	|	Отгрузки.Дата КАК Дата,
	               	|	Отгрузки.ТранспортноеСредство КАК ТранспортноеСредство,
	               	|	Отгрузки.Контрагент КАК Контрагент,
	               	|	Отгрузки.Количество,
	               	|	ЦеныЗаПеревозкуПоТССрезПоследних.ЦенаЗаЕдиницу КАК Цена
	               	|ИЗ
	               	|	Отгрузки КАК Отгрузки
	               	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныЗаПеревозкуПоТС.СрезПоследних(&КонДата, ) КАК ЦеныЗаПеревозкуПоТССрезПоследних
	               	|		ПО Отгрузки.ТранспортноеСредство = ЦеныЗаПеревозкуПоТССрезПоследних.ТС
	               	|
	               	|УПОРЯДОЧИТЬ ПО
	               	|	Дата,
	               	|	Контрагент,
	               	|	ТранспортноеСредство";
	
	Запрос.УстановитьПараметр("Организация", объект.Организация);
	Запрос.УстановитьПараметр("Транспорт", 	Транспорт);
	Запрос.УстановитьПараметр("НачДата", 	ДатаНач);
	Запрос.УстановитьПараметр("КонДата", 	ДатаКон);
	
	ТабЧасть.Очистить();
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл 
		НовСтр = ТабЧасть.добавить();
		ЗаполнитьЗначенияСвойств(НовСтр,Результат);
		НовСтр.Номенклатура = Справочники.Номенклатура.УслугиАвтотранспортаПоДоставкеАсфальта;
		НовСтр.Содержание = Формат(Результат.Дата, "ДФ=dd.MM.yyyy")+", "+ Результат.ТранспортноеСредство.Наименование+", "+ Результат.Контрагент.Наименование;
	КонецЦикла;
	
//	ТабЧасть.Загрузить(Результат.Выгрузить());
	
	Док = Объект;
	Для Каждого СтрДок из ТабЧасть Цикл 
		
		//ОбработкаТабличныхЧастей.ЗаполнитьСодержаниеТабЧасти(СтрДок, Док);
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрДок, Док); 
		
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрДок, Док, 1);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрДок, Док);
		
		док.ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрДок, "Услуги", Истина, Истина);
		
	КонецЦикла;
    док.УслугиНачДата = ДатаНач;
	док.УслугиКонДата = ДатаКон;
	
КонецПроцедуры
