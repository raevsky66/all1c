Перем мОчищатьТаблицуПередЗаполнением Экспорт;
Перем мВыполнитьЗаполнение Экспорт;

Процедура Инициализировать(Объект, ИмяТабличнойЧасти, ТабличноеПолеОбъекта) Экспорт
	
	ТабЧасть	= Объект[ИмяТабличнойЧасти];
	
	ДатаНач = "";
	ВвестиДату(ДатаНач,"Введите начальную дату",ЧастиДаты.Дата);
	
	ДатаКон = "";
	ВвестиДату(ДатаКон,"Введите конечную дату",ЧастиДаты.Дата);
	
	Если НЕ ЗначениеЗаполнено(ДатаНач) ИЛИ Не ЗначениеЗаполнено(ДатаКон) Тогда 
		Предупреждение("Не указаны даты...");
		возврат;
	КонецЕсли;
	
	ДатаКон = КонецДня(ДатаКон);
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Сообщить("Не выбрана организация");
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Сообщить("Не выбран контрагент");
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ПоступлениеТоваровУслугТовары.Количество) КАК Количество,
	|	СУММА(ПоступлениеТоваровУслугТовары.КоличествоПоДокументу) КАК КоличествоПоДокументу,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Грузоотправитель,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Контрагент КАК Поставщик
	|ПОМЕСТИТЬ ПришлоКоличество
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка.Дата МЕЖДУ &НачДата И &КонДата
	|	И ПоступлениеТоваровУслугТовары.Ссылка.Грузоотправитель = &Контрагент
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугТовары.Ссылка.Грузоотправитель,
	|	ПоступлениеТоваровУслугТовары.Ссылка.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПришлоКоличество.Количество,
	|	ПришлоКоличество.КоличествоПоДокументу,
	|	ЕСТЬNULL(ЦеныЗаПеревозкуСрезПоследних.ЦенаЗаЕдиницу, 0) КАК Цена,
	|	&УслугаДоставкиИнертныхМатериалов КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ПришлоКоличество.Поставщик ЕСТЬ NULL 
	|			ТОГДА ""Поступление по не указанному поставщищику""
	|		ИНАЧЕ ""По "" + ПришлоКоличество.Поставщик.Наименование
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	ПришлоКоличество КАК ПришлоКоличество
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныЗаПеревозку.СрезПоследних(&КонДата, ) КАК ЦеныЗаПеревозкуСрезПоследних
	|		ПО ПришлоКоличество.Грузоотправитель = ЦеныЗаПеревозкуСрезПоследних.Контрагент
	|			И ПришлоКоличество.Поставщик = ЦеныЗаПеревозкуСрезПоследних.Поставщик";
	
	Запрос.УстановитьПараметр("Контрагент", 	Объект.Контрагент);
	Запрос.УстановитьПараметр("НачДата", 	ДатаНач);
	Запрос.УстановитьПараметр("КонДата", 	ДатаКон);
	Запрос.УстановитьПараметр("УслугаДоставкиИнертныхМатериалов", 	Справочники.Номенклатура.УслугаДоставкиИнертныхМатериалов);
	
	
	Результат = Запрос.Выполнить();
	
	ТабЧасть.Загрузить(Результат.Выгрузить());
	
	Док = Объект;
	Для Каждого СтрДок из ТабЧасть Цикл 
		
		//ОбработкаТабличныхЧастей.ЗаполнитьСодержаниеТабЧасти(СтрДок, Док);
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрДок, Док); 
		
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрДок, Док, 1);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрДок, Док);
		
		док.ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрДок, "Услуги", Истина, Истина);
		
	КонецЦикла;
 
КонецПроцедуры
